#!/usr/bin/env perl
use Mojolicious::Lite;
use Mojo::JSON            qw(encode_json decode_json);
use Mojo::Util            qw(slurp spurt);
use Data::Dumper;
use File::Spec::Functions qw(catfile);
use List::Util            qw(pairmap);
use Lock::File            qw(lockfile);
use lib                   qw(Commit-Rater);
use CommitRater;


plugin Minion => {File => 'minion.db'};

app->minion->add_task(rate_commits => sub
{
    my ($job, $remote, $output) = @_;

    my $lock = lockfile("$output.lock", {blocking => 0, remove => 1}) or return;

    if (!-e $output)
    {
        my $stats = CommitRater->new($remote)->rate;
        spurt encode_json($stats) => $output;
    }
});


sub load_stats
{
    my ($c, $user, $repo, $no_job) = @_;

    my $file  = catfile 'stats', "$user.$repo.json";
    my $stats = eval { decode_json slurp $file };

    if (!$stats && !$no_job)
    {
        print Dumper($c->minion->backend->_jobs);
        $c->minion->enqueue(rate_commits => [
            "https://github.com/$user/$repo/",
            $file,
        ]);
    }

    return $stats;
}


get '/' => sub { shift->render(template => 'index') };


get '/repo/:user/:repo' => sub
{
    my $c = shift;
    my ($user, $repo) = @{$c->stash}{'user', 'repo'};
    $c->render_later;

    $c->ua->head("https://github.com/$user/$repo/" => sub
    {
        my ($ua, $tx) = @_;
        return $c->render(text => Dumper($tx)) if not $tx->success;
        #return $c->render(status => $tx->res->code) if not $tx->success;

        $c->title("$user/$repo",
            user  => $user,
            repo  => $repo,
            stats => load_stats($c, $user, $repo),
        );
        $c->render(template => 'repo');
    });
};


get '/res/repo/:user/:repo' => sub
{
    my $c = shift;
    my ($user, $repo) = @{$c->stash}{'user', 'repo'};
    $c->render_later;

    $c->ua->head("https://github.com/$user/$repo/" => sub
    {
        my ($ua, $tx) = @_;
        if (!$tx->success)
        {
            return $c->render(
                text   => 'Error from Github: ' . $tx->res->code,
                status => $tx->res->code,
            );
        }

        if (load_stats($c, $user, $repo, 1))
        {   $c->render(text => 'OK') }
        else
        {   $c->render(text => 'Fetching', status => 202) }
    });
};


app->start;
